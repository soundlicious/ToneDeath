apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "dev.expositopablo.tonedeath"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "FABRICAPIKEY", "${rootProject.fabricApiKey}"
            resValue "string", "ADMOB_TOKEN", "${rootProject.adMobAppToken}"
            resValue "string", "ADMOB_RECOMPENSE_TOKEN", "${rootProject.adMobRecompenseAppTokenTest}"
            resValue "string", "ADMOB_BANNER_TOKEN", "${rootProject.adMobBannerAppTokenTest}"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "FABRICAPIKEY", "${rootProject.fabricApiKey}"
            resValue "string", "ADMOB_TOKEN", "${rootProject.adMobAppToken}"
            resValue "string", "ADMOB_RECOMPENSE_TOKEN", "${rootProject.adMobRecompenseAppToken}"
            resValue "string", "ADMOB_BANNER_TOKEN", "${rootProject.adMobBannerAppToken}"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //region SupportLib
    implementation "com.android.support:support-v4:$supportLib"
    implementation "com.android.support:customtabs:$supportLib"
    implementation "com.android.support:support-v13:$supportLib"
    implementation "com.android.support:appcompat-v7:$supportLib"
    implementation "com.android.support:palette-v7:$supportLib"
    implementation "com.android.support:recyclerview-v7:$supportLib"
    implementation "com.android.support:cardview-v7:$supportLib"
    implementation "com.android.support:design:$supportLib"
    implementation "com.android.support.constraint:constraint-layout:$constraintLib"
    //endregion

    //region UI
    implementation "blue.aodev:material-values:$materialValues"
    implementation "com.jakewharton:butterknife:$butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife"
    implementation "com.github.thomper:sweet-alert-dialog:$sweetAlertDialog"
    //endregion

    //region ArchComponent
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    androidTestImplementation "android.arch.persistence.room:testing:$roomVersion"
    implementation "android.arch.lifecycle:extensions:$archLifecycleVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archLifecycleVersion"
    //endregion

    //region Dependency Injection
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger"
    //endregion

    //region GoogleServices
    implementation "com.google.firebase:firebase-core:$firebase"
    implementation "com.google.android.gms:play-services-ads:$playService"
    //endregion

    //region Testing && Debug
    implementation "com.jakewharton.timber:timber:$timber"
    testImplementation "junit:junit:$junit"
    androidTestImplementation "com.android.support.test:runner:$runner"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary"
    debugImplementation "com.amitshekhar.android:debug-db:$debugDB"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanary"
    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics"){
        transitive = true
    }
    //endregion
}

apply plugin: 'com.google.gms.google-services'
